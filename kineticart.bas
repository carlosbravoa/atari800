10 REM KINETIC ART BY NEIL HARRIS
20 GRAPHICS 10
30 DIM A(3,50)
35 FOR L=0 TO 3:FOR M=0 TO 50:A(L,M)=0:NEXT M:NEXT L 
40 HUE=INT(RND(1)*8+1):POKE 704+HUE,INT(RND(1)*8)*16+INT(RND(1)*4+4)
50 X1=INT(RND(1)*80):X2=INT(RND(1)*80) :Y1=INT(RND(1)*192):Y2=INT(RND(1)*192)
60 COLOR 0:PLOT A(0,WHICH),A(1,WHICH) :DRAWTO A(2,WHICH),A(3,WHICH)
70 BOUNCE=BOUNCE-1:IF BOUNCE>0 THEN 90
80 BOUNCE=INT(RND(1)*10+10):BX1=INT(RND(1)*9-4):BX2=INT(RND(1)*9-4):BY1=INT(RND(1)*13-6):BY2=INT(RND(1)*13-6)
90 CHANGE=CHANGE-1:IF CHANGE>0 THEN 110
100 CHANGE=INT(RND(1)*10+5):HUE=INT(RND(1)*8+1):POKE 704+HUE,INT(RND(1)*256)
110 COLOR HUE:PLOTX1,Y1:DRAWTOX2,Y2
120 A(0,WHICH)=X1:A(1,WHICH)=Y1:A(2,WHICH)=X2:A(3,WHICH)=Y2
130 WHICH=WHICH+1:IF WHICH>50 THEN WHICH=0
140 X1=X1+BX1:IF X1<0 OR X1>79 THEN BX1=-BX1:GOTO 140 
150 X2=X2+BX2:IF X2<0 OR X2>79 THEN BX2=-BX2:GOTO 150 
160 Y1=Y1+BY1:IF Y1<0 OR Y1>191 THEN BY1=-BY1:GOTO 160 
170 Y2=Y2+BY2:IF Y2<0 OR Y2>191 THEN BY2=-BY2:GOTO 170 
180 GOTO 60

